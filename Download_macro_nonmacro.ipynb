{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Three SAS Programs\n",
    "* Nonmacro program (with a single macro variable) for XPT data download ( 1 file at a time)\n",
    "* Nonmacro program (with a single macro variable) for CPT data download ( 1 file at a time)\n",
    "* Macro program "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "* Single_CPT_Download.sas;\n",
      "/************************************************************************************\n",
      "* TO DOWNLOAD, UNZIP AND RESTORE a SAS data set from only a single CPORT-created \n",
      "* trnsposrt file only one at a time) from the MEPS website     \n",
      "*\n",
      "* Just create a macro variable for the data file you are interested in\n",
      "* (%LET file = h206a;)   That's it! There is no macro program involved here. \n",
      "*\n",
      "* The output files are saved in the WORK folder. \n",
      "* CAUTION: Downloaed unzipped/zipped/restored SAS data files will only exist \n",
      "* in the current SAS session.\n",
      "*\n",
      "* Adjust the code as appropriate to save the files permanently.\n",
      "*\n",
      "* Written by: Pradip Muhuri                                                         \n",
      "***************************************************************************************/\n",
      "\n",
      "proc datasets library=work kill;run;quit;\n",
      "%LET file=h206a; \n",
      "%put &=file;\n",
      "/* use pathname function to get work library's path and store in macro variable wpath */\n",
      "%let wpath=%sysfunc(pathname(work));\n",
      "\n",
      "*** Task 1: Download a zipped SAS transport file from the MEPS web site;\n",
      "\n",
      "/* use wpath macro variable and append filename to it in filename statement */\n",
      "*options symbolgen mprint mlogic merror;\n",
      "Filename GoThere \"&wpath\\&file.ssp.zip\";\n",
      "proc http \n",
      "   url=\"https://meps.ahrq.gov/data_files/pufs/&file.ssp.zip\" \n",
      "   out=GoThere;\n",
      "run;\n",
      "/***********************************************************************\n",
      "* Acknowledgements:\n",
      "* https://blogs.sas.com/content/sasdummy/2015/05/11/using-filename-zip-to-unzip-and-read-data-files-in-sas/\n",
      "* Read the \"members\" (files) from the ZIP file\n",
      "***************************************************************************************************************/\n",
      "/*Assign a fileref wth the ZIP method */\n",
      "filename inzip zip \"&wpath\\&file.ssp.zip\";\n",
      " \n",
      "data contents(keep=memname);\n",
      " length memname $200;\n",
      " fid=dopen(\"inzip\");\n",
      " if fid=0 then\n",
      "  stop;\n",
      " memcount=dnum(fid);\n",
      " do i=1 to memcount;\n",
      "  memname=dread(fid,i);\n",
      "  output;\n",
      " end;\n",
      " rc=dclose(fid);\n",
      " call symputx ('memname', memname);\n",
      "run;\n",
      " %put &=memname;\n",
      "/* create a report of the ZIP contents */\n",
      "title \"Files in the ZIP file\";\n",
      "proc print data=contents noobs N;\n",
      "run;\n",
      " \n",
      "*** Task 2: Unzip the SAS transport data file (Member name in upper case);\n",
      "filename inzip zip \"&wpath\\&file.ssp.zip\" ;\n",
      "filename sit \"&wpath\\&memname\" ;\n",
      "/* hat tip: \"data _null_\" on SAS-L */\n",
      " data _null_; \n",
      "infile inzip(&memname) recfm=F lrecl=256 length=len_length eof=eof unbuf;\n",
      "file sit lrecl=256 recfm=N; \n",
      "input @; put _infile_ $varying256. len_length;\n",
      "return; \n",
      "eof: stop;\n",
      "run;\n",
      "\n",
      "*** Task 3: Restore the SAS data set from the PROC CPORT-created SAS transport file;\n",
      "filename cpt \"&wpath\\&memname\" ;\n",
      "proc cimport infile=cpt lib=work;\n",
      "run;\n",
      "\n",
      "/*********************************************************\n",
      "* Show the metadata for the SAS data set restored from the transport file (optional)               \n",
      "***********************************************************/\n",
      "proc sql;\n",
      "select memname,\n",
      "        nobs format =comma9.\n",
      "       ,nvar format =comma9.\n",
      "\t   ,DATEPART(crdate) format date9. as Date_created label='Creation Date'\n",
      "\t   ,TIMEPART(crdate) format timeampm. as Time_created label='Creation Time'\n",
      "from dictionary.tables\n",
      " where libname='WORK' and memname like upper(\"&file\");\n",
      " quit;\n",
      "\n",
      "\n",
      "\n"
     ]
    }
   ],
   "source": [
    "fd = open(r'U:\\_Treasure_SAS\\Single_CPT_Download.sas')\n",
    "print(fd.read())\n",
    "fd.close()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "* Single_XPT_Download.sas;\n",
      "/************************************************************************************\n",
      "* TO DOWNLOAD, UNZIP AND RESTORE a SAS data set from only a single XPORT-created \n",
      "* trnsposrt file only one at a time) from the MEPS website     \n",
      "*\n",
      "* Just create a macro variable for the data file you are interested in\n",
      "* (%LET file = h183;)   That's it! There is no macro program involved here. \n",
      "*\n",
      "* The output files are saved in the WORK folder. \n",
      "* CAUTION: Downloaed unzipped/zipped/restored SAS data files will only exist \n",
      "* in the current SAS session.\n",
      "*\n",
      "* Adjust the code as appropriate to save the files permanently.\n",
      "*\n",
      "* Written by: Pradip Muhuri                                                         \n",
      "***************************************************************************************/\n",
      "\n",
      "proc datasets library=work kill;run;quit;\n",
      "%LET file=h183; \n",
      "%put &=file;\n",
      "/* use pathname function to get work library's path and store in macro variable wpath */\n",
      "%let wpath=%sysfunc(pathname(work));\n",
      "\n",
      "*** Task 1: Download a zipped SAS transport file from the MEPS web site;\n",
      "\n",
      "/* use wpath macro variable and append filename to it in filename statement */\n",
      "*options symbolgen mprint mlogic merror;\n",
      "Filename GoThere \"&wpath\\&file.ssp.zip\";\n",
      "proc http \n",
      "   url=\"https://meps.ahrq.gov/data_files/pufs/&file.ssp.zip\" \n",
      "   out=GoThere;\n",
      "run;\n",
      "/***********************************************************************\n",
      "* Acknowledgements:\n",
      "* https://blogs.sas.com/content/sasdummy/2015/05/11/using-filename-zip-to-unzip-and-read-data-files-in-sas/\n",
      "* Read the \"members\" (files) from the ZIP file\n",
      "***************************************************************************************************************/\n",
      "/*Assign a fileref wth the ZIP method */\n",
      "filename inzip zip \"&wpath\\&file.ssp.zip\";\n",
      " \n",
      "data contents(keep=memname);\n",
      " length memname $200;\n",
      " fid=dopen(\"inzip\");\n",
      " if fid=0 then\n",
      "  stop;\n",
      " memcount=dnum(fid);\n",
      " do i=1 to memcount;\n",
      "  memname=dread(fid,i);\n",
      "  output;\n",
      " end;\n",
      " rc=dclose(fid);\n",
      " call symputx ('memname', memname);\n",
      "run;\n",
      " %put &=memname;\n",
      "/* create a report of the ZIP contents */\n",
      "title \"Files in the ZIP file\";\n",
      "proc print data=contents noobs N;\n",
      "run;\n",
      " \n",
      "*** Task 2: Unzip the SAS transport data file (Member name in upper case);\n",
      "filename inzip zip \"&wpath\\&file.ssp.zip\" ;\n",
      "filename sit \"&wpath\\&memname\" ;\n",
      "/* hat tip: \"data _null_\" on SAS-L */\n",
      " data _null_; \n",
      "infile inzip(&memname) recfm=F lrecl=256 length=len_length eof=eof unbuf;\n",
      "file sit lrecl=256 recfm=N; \n",
      "input @; put _infile_ $varying256. len_length;\n",
      "return; \n",
      "eof: stop;\n",
      "run;\n",
      "\n",
      "*** Task 3: Restore the SAS data set from the XPORT-engine-created SAS transport file;\n",
      "\n",
      "libname xpt xport \"&wpath\\&memname\";\n",
      " proc copy in=xpt out=work; \n",
      "run;\n",
      "\n",
      "/*********************************************************\n",
      "* Show the metadata for the SAS data set restored from the transport file (optional)               \n",
      "***********************************************************/\n",
      "proc sql;\n",
      "select memname,\n",
      "        nobs format =comma9.\n",
      "       ,nvar format =comma9.\n",
      "\t   ,DATEPART(crdate) format date9. as Date_created label='Creation Date'\n",
      "\t   ,TIMEPART(crdate) format timeampm. as Time_created label='Creation Time'\n",
      "from dictionary.tables\n",
      " where libname='WORK' and memname like upper(\"&file\");\n",
      " quit;\n",
      "\n",
      "\n",
      "\n"
     ]
    }
   ],
   "source": [
    "fd = open(r'U:\\_Treasure_SAS\\Single_XPT_Download.sas')\n",
    "print(fd.read())\n",
    "fd.close()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "*Automate_Load_Oct06_rev.sas;\n",
      "/***********************************************************************\n",
      "This program  automates:\n",
      "- downloading of zipped SAS Trasport files from the MEPS website\n",
      "- unzipping of those files (created by XPORT Engine vs. PROC CPORT)\n",
      "- restoring to SAS data sets\n",
      "\t\n",
      "Written by: Pradip Muhuri - Revised 10/06/2020  \n",
      "Acknowledgements: Thanks to SAS(R) Institute for providing technical support \n",
      "to the revision/expansion of the original program.\n",
      "************************************************************************/\n",
      "options  nosymbolgen nomlogic nomprint; /* Options may be turned on when needed */\n",
      "\n",
      "/**************************************************************************\n",
      "* The following two statements delete the existing files in the libraries  *\n",
      "* specified. The user has the option to delete or comment out them        *            *\n",
      "***************************************************************************/\n",
      "libname librefs     ('C:\\Data\\zipfiles',\n",
      "                     'C:\\Data\\xptfiles',\n",
      "\t\t\t\t\t 'C:\\Data\\cptfiles',\n",
      "\t\t\t\t\t 'C:\\Data\\MySDS');\n",
      "proc datasets library=librefs kill;run;quit;\n",
      "\n",
      "/* Create 4 global macro variables for naming folders that were already created  */\n",
      "\n",
      "%let zipfiles = C:\\Data\\zipfiles ;  /* Save downloaded zipped files */\n",
      "%let xptfiles = C:\\Data\\xptfiles;   /* Save extracted PROC COPY/XPORT-created transport files */\n",
      "%let cptfiles = C:\\Data\\cptfiles;   /* Save extracted PROC CIMPORT-created transport files */\n",
      "%let MySDS =    C:\\Data\\MySDS;         /* Save SAS data sets restored from transport files */\n",
      "\n",
      "/****************************************************************************\n",
      "* Create 3 global macro variables containing \n",
      "*\t- a list of XPORT engine-created transport file names \n",
      "*\t- a list of PROC CPORT-created transport file names \n",
      "*   - a list macro variables of macro variables to itearte over\n",
      "*****************************************************************************/\n",
      "/* One has the option to leave blanks for the any one of the macro variables*/;\n",
      "%let xpt_files = h183 h193 h202; \n",
      "%let cpt_files = h201 h209 h206a;\n",
      "%let files = &xpt_files &cpt_files;\n",
      "\n",
      "%put &=xpt_files;\n",
      "%put &=cpt_files;\n",
      "%put &=files;\n",
      "\n",
      "%macro load_MEPS / minoperator ;\n",
      "%local j;\n",
      "%do j=1 %to %sysfunc(countw(&files));\n",
      "   /*******************************************************************   \n",
      "   ** Task 1: Download zipped SAS transport files from the MEPS web site\n",
      "   ** using PROC HTTP\n",
      "   **********************************************************************/\n",
      "\tfilename inzip1 \"&zipfiles.\\%scan(&files, &j)ssp.zip\";\n",
      "\tproc http \n",
      "\t url=\"https://meps.ahrq.gov/data_files/pufs/%scan(&files, &j)ssp.zip\"  \n",
      "\t out=inzip1;\n",
      "\trun;\n",
      "\n",
      "    /*****************************************************************************\n",
      "\t* Task 2: Unzip them into the folder of your choice \n",
      "\t* (defined by a macro variable - created earlier)\n",
      "\t* Using the FILENAME ZIP method\n",
      "    ******************************************************************************/\n",
      "\t/*\n",
      "\tFrom: https://blogs.sas.com/content/sasdummy/2015/05/11/using-filename-zip-to-unzip-and-read-data-files-in-sas/ \n",
      "\t*/\n",
      "\t\n",
      "\t/* Read the \"members\" (files) from the ZIP file */\n",
      "\tfilename inzip2 zip \"&zipfiles.\\%scan(&files, &j)ssp.zip\"; \n",
      "\tdata contents(keep=memname isFolder);\n",
      "\t length memname $200 isFolder 8;\n",
      "\t fid=dopen(\"inzip2\");\n",
      "\t if fid=0 then\n",
      "\t  stop;\n",
      "\t memcount=dnum(fid);\n",
      "\t do i=1 to memcount;\n",
      "\t  memname=dread(fid,i);\n",
      "\t  /* check for trailing / in folder name */\n",
      "\t  isFolder = (first(reverse(trim(memname)))='/');\n",
      "\t  output;\n",
      "\t end;\n",
      "\t rc=dclose(fid);\n",
      "\t /* this could be automated if more than one file is expected in a zip */\n",
      "\t call symputx('memname',memname);\n",
      "\trun;\n",
      "\t %PUT &=MEMNAME;\n",
      "\t/* create a report of the ZIP contents */\n",
      "\ttitle \"Files in the ZIP file\";\n",
      "\tproc print data=contents noobs N;\n",
      "\trun;\n",
      "\n",
      "        \n",
      "\t%if &xpt_files ne %then %do; \n",
      "\t   %if %eval(%scan(&files, &j) # &xpt_files) %then  %do;\n",
      "\t  filename sit \"&xptfiles.\\&memname\" ;\n",
      "      %end;\n",
      "    %end;\n",
      "\n",
      "\t%if &cpt_files ne %then %do; \n",
      "\t   %IF %eval(%scan(&files, &j)  # &cpt_files) %then %do;\n",
      "\t       filename sit \"&cptfiles.\\&memname\" ;\n",
      "\t  %end;\n",
      "    %end;\n",
      "\n",
      "\t/* hat tip: \"data _null_\" on SAS-L */\n",
      "\tdata _null_;\n",
      "\t   /* using member syntax here */\n",
      "\t   infile inzip2(&memname.) \n",
      "\t       lrecl=256 recfm=F length=length eof=eof unbuf;\n",
      "    \t   file sit lrecl=256 recfm=N;\n",
      "\t   input;\n",
      "\t   put _infile_ $varying256. length;\n",
      "\t   return;\n",
      "\t eof:\n",
      "\t   stop;\n",
      "\trun;\n",
      "\t\n",
      "  /*******************************************************************\t \n",
      "  * Task 3: Restore the files in the original form as SAS data sets\n",
      "  ********************************************************************/\n",
      "\n",
      " /* Use PROC COPY to restore the SAS Transport files for 2017 or prior years */\n",
      "%if &xpt_files ne %then %do; \n",
      " %IF %eval(%scan(&files, &j)  # &xpt_files) %then %do;\n",
      " \t libname xpt xport \"&xptfiles.\\%scan(&files, &j).ssp\";\n",
      "\t libname sds \"&MySDS\";  \n",
      "      proc copy in=xpt out=sds; run;\n",
      "  %end;\n",
      " %end;\n",
      "  /* Use PROC CIMPORT to restore the SAS Transport files for 2018 or later years */\n",
      "%if &cpt_files ne %then %do; \n",
      "  %if %eval(%scan(&files, &j) #  &cpt_files) %then %do;\n",
      "  \t Filename cpt \"&cptfiles.\\%scan(&files, &j).ssp\"; \n",
      "     libname sds base \"&MySds\"; \n",
      "         PROC CIMPORT INFILE=cpt LIBRARY=sds;\n",
      "     RUN;\n",
      "   %end;\n",
      " %end;\n",
      "%end;\n",
      "%mend;\n",
      "%load_MEPS\n",
      "\n",
      "\n",
      "/*********************************************************\n",
      "* Create a summary table for all the SAS data sets restored \n",
      "* from Transport files (outside of the above macro (optional)               \n",
      "***********************************************************/\n",
      "proc sql;\n",
      "select memname,\n",
      "        nobs format =comma9.\n",
      "       ,nvar format =comma9.\n",
      "\t   ,DATEPART(crdate) format date9. as Date_created label='Creation Date'\n",
      "\t   ,TIMEPART(crdate) format timeampm. as Time_created label='Creation Time'\n",
      "from dictionary.tables\n",
      " where libname='SDS' and memname like \"H%\";\n",
      " quit;\n",
      "\n",
      "\n"
     ]
    }
   ],
   "source": [
    "fd = open(r'U:\\_Treasure_SAS\\Automate_Load_Oct06_rev.sas')\n",
    "print(fd.read())\n",
    "fd.close()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "name": ""
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
